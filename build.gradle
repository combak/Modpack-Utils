/* *************************************************************************************************************
 * Copyright (c) 2017 DerOli82 <https://github.com/DerOli82>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see:
 *
 * https://www.gnu.org/licenses/lgpl-3.0.html
 ************************************************************************************************************ */
 
buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/releases/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
        classpath "org.ajoberstar:gradle-git:1.6.0"
    }
}
apply plugin: "org.ajoberstar.grgit"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"

//Load properties
file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

//Get Github commit hash
task tagRelease {
  description = "Tags the current head with the project\'s version."
  doLast {
    grgit.tag.add {
      name = version
      message = "Release of ${version}"
    }
  }
}

//Version
version = "${config.minecraft.version}-${config.mod.version}-${grgit.head().abbreviatedId}"
group= "de.alaoli.games.minecraft.mods.modpackutils" 
archivesBaseName = "modpackutils"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

dependencies {
    compile project( ":ui-library" )
    compile project( ":common-library" )
    compile "com.mashape.unirest:unirest-java:${config.unirest.version}"
}

shadowJar {
    relocate "org.json", "libs.org.json"
    relocate "org.apache", "libs.org.apache"
    relocate "com.mashape", "libs.com.mashape"
    relocate "de.alaoli.games.minecraft.mods.lib", "libs.de.alaoli.games.minecraft.mods.lib"
}

minecraft {
    mappings = "${config.forge.mappings}"
    version = "${config.forge.version}"
    runDir = "run"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
                
        // replace version and mcversion
        expand "version":project.version, "mcversion":project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

reobf {
    shadowJar {}
}
